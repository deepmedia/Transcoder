apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Required by bintray
version = '0.8.0'
group = 'com.otaliastudios'
archivesBaseName = 'transcoder'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName project.version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.mockito:mockito-android:2.28.2'

    api 'com.otaliastudios.opengl:egloo:0.4.0'
    api "androidx.annotation:annotation:1.1.0"
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Transcoder'
            description 'Accelerated video transcoding using Android MediaCodec API without native code (no LGPL/patent issues)'
            url 'https://github.com/natario1/Transcoder'

            packaging 'aar'
            groupId project.group
            artifactId 'transcoder'
            version project.version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection 'https://github.com/natario1/Transcoder.git'
                developerConnection 'https://github.com/natario1/Transcoder.git'
                url 'https://github.com/natario1/Transcoder'

            }
            developers {
                developer {
                    id = 'natario'
                    name 'Mattia Iavarone'
                }
            }
        }
    }
}

def bintrayUser
def bintrayKey
def travis = System.getenv("TRAVIS")
if (travis) {
    bintrayUser = System.getenv("BINTRAY_USER")
    bintrayKey = System.getenv("BINTRAY_KEY")
} else {
    Properties props = new Properties()
    props.load(project.rootProject.file('local.properties').newDataInputStream())
    bintrayUser = props.getProperty('bintray.user')
    bintrayKey = props.get('bintray.key')
}

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'Transcoder'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/natario1/Transcoder.git'
        publish = true
        override = true
        version {
            name = project.version
            desc = 'Transcoder v. '+project.version
            released = new Date()
            vcsTag = 'v'+project.version
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += project.files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
    project.android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            classpath += files(variant.javaCompile.classpath)
        }
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}